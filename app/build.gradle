apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.parse'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.parse.com/repo' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.parse.tools:gradle:1.+'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.imaginarysolutions.givelife"
        minSdkVersion 15
        targetSdkVersion 22

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        buildConfigField "String", "TWITTER_KEY", "\"${twitterKey}\""
        buildConfigField "String", "TWITTER_SECRET", "\"${twitterSecret}\""

        resValue "string", "parse_app_id", parseAppId
        resValue "string", "parse_client_key", parseClientKey
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: 'Parse-*.jar')
    compile fileTree(dir: 'libs', include: 'ParseCrashReporting-*.jar')
    compile 'com.parse.bolts:bolts-android:1.2.0'

    compile('com.twitter.sdk.android:twitter:1.4.0@aar') {
        transitive = true;
    }

    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.1.1'
    // An interactive indicator to navigate between the different pages of a ViewPager
    compile 'com.jpardogo.materialtabstrip:library:1.0.9'
    // A draggable sliding up panel
    compile 'com.sothree.slidinguppanel:library:3.0.0'
    // View "injection" library for Android
    compile 'com.jakewharton:butterknife:6.1.0'
}

parse {
    applicationId parseAppId
    masterKey parseMasterKey

    // Make symbol upload automatic. Otherwise, use e.g. ../gradlew parseUploadSymbolsDebug;
    uploadSymbols true
}
